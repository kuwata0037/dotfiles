# Enable Powerlevel10k instant prompt. Should stay close to the top of ~/.config/zsh/.zshrc.
# Initialization code that may require console input (password prompts, [y/n]
# confirmations, etc.) must go above this block; everything else may go below.
if [[ -r "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh" ]]; then
    source "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh"
fi

#
# Environment
#
### General
export LANG=en_US.UTF-8
export TERM=xterm-256color
export WORDCHARS='*?_-.[]~=&;!#$%^(){}<>'

export TMUX_PLUGIN_MANAGER_PATH=$XDG_CONFIG_HOME/tmux/plugins

### Homebrew
case $OSTYPE in
    linux*)
        export HOMEBREW_PREFIX="/home/linuxbrew/.linuxbrew"
        export HOMEBREW_CELLAR="/home/linuxbrew/.linuxbrew/Cellar"
        export HOMEBREW_REPOSITORY="/home/linuxbrew/.linuxbrew/Homebrew"
        export MANPATH="/home/linuxbrew/.linuxbrew/share/man${MANPATH+:$MANPATH}:"
        export INFOPATH="/home/linuxbrew/.linuxbrew/share/info:${INFOPATH:-}"
        export LD_LIBRARY_PATH="/home/linuxbrew/.linuxbrew/lib:${LD_LIBRARY_PATH:-}"
        ;;
esac

### Go
export GOPATH=$HOME/.go

### Sccache
if type sccache > /dev/null; then
    export RUSTC_WRAPPER=$(which sccache)
fi

### Path
path=(
    $GOPATH/bin(N-/)
    $HOME/.cargo/bin(N-/)
    $HOMEBREW_PREFIX/{sbin,bin}(N-/)
    $HOME/.local/bin(N-/)
    /usr/local/{sbin,bin}(N-/)
    /usr/{sbin,bin}(N-/)
    /{sbin,bin}(N-/)
    $path
)
typeset -Ug PATH

#
# Plugin Manager
#
### Added by Zinit's installer
if [[ ! -f ${ZDOTDIR}/.zinit/bin/zinit.zsh ]]; then
    print -P "%F{33}▓▒░ %F{220}Installing %F{33}DHARMA%F{220} Initiative Plugin Manager (%F{33}zdharma/zinit%F{220})…%f"
    command mkdir -p "${ZDOTDIR}/.zinit" && command chmod g-rwX "${ZDOTDIRE}/.zinit"
    command git clone https://github.com/zdharma/zinit "${ZDOTDIR}/.zinit/bin" && \
        print -P "%F{33}▓▒░ %F{34}Installation successful.%f%b" || \
        print -P "%F{160}▓▒░ The clone has failed.%f%b"
fi

source "${ZDOTDIR}/.zinit/bin/zinit.zsh"
autoload -Uz _zinit
(( ${+_comps} )) && _comps[zinit]=_zinit

# Load a few important annexes, without Turbo
# (this is currently required for annexes)
zinit light-mode for \
    zinit-zsh/z-a-rust \
    zinit-zsh/z-a-as-monitor \
    zinit-zsh/z-a-patch-dl \
    zinit-zsh/z-a-bin-gem-node

### End of Zinit's installer chunk

#
# Alias
#
alias e='emacsclient -nw -a ""'
alias ekil='emacsclient -e "(kill-emacs)"'
alias g='git'
alias mk='command mkdir -p'

zinit ice as"program" from"gh-r" mv"bat* -> bat" pick"bat/bat"
zinit light sharkdp/bat
alias cat='bat'

zinit ice as"program" from"gh-r" pick"*/exa"
zinit light ogham/exa
alias ls='exa --icons'
alias ll='exa -lh --icons'
alias la='exa -alh --icons'

zinit ice from"gh-r" as"program" pick"*/ghq"
zinit light x-motemen/ghq
alias gg='ghq get'

zinit ice lucid wait"0"
zinit light "mollifier/cd-gitroot"
alias cdr='cd-gitroot'

#
# Keybind
#
bindkey "^[[H" beginning-of-line
bindkey "^[[F" end-of-line
bindkey "^[[3~" delete-char

zinit ice from"gh-r" as"program"
zinit light junegunn/fzf-bin
zinit light mollifier/anyframe
bindkey "^g" anyframe-widget-cd-ghq-repository
# bindkey "^r" anyframe-widget-put-history
bindkey "^xb" anyframe-widget-checkout-git-branch
bindkey "^xk" anyframe-widget-kill
zstyle ":anyframe:selector:fzf:" command 'fzf --reverse'
zstyle ":anyframe:selector:fzf-tmux:" command 'fzf-tmux --reverse'

#
# Completion
#
autoload -Uz compinit && compinit

zinit ice wait lucid blockf
zinit light zsh-users/zsh-completions

zinit wait lucid blockf is-snippet as"completion" for \
    OMZP::docker/_docker \
    OMZP::docker-compose/_docker-compose \
    OMZP::rust/_rust \
    OMZP::cargo/_cargo \
    OMZP::rustup/_rustup

setopt magic_equal_subst
setopt menu_complete
setopt no_flow_control

zstyle ':completion:*' format '%B%F{blue}%d%f%b'
zstyle ':completion:*' group-name ''
zstyle ':completion:*' matcher-list 'm:{a-z}={A-Z}'
zstyle ':completion:*:default' menu select=2

### aws cli
if type aws > /dev/null && type aws_completer > /dev/null; then
    autoload bashcompinit && bashcompinit
    complete -C aws_completer aws
fi

### cd
export ENHANCD_HOOK_AFTER_CD=ls
export ENHANCD_DISABLE_HOME=1
export ENHANCD_DOT_ARG="."
zinit ice wait lucid pick"init.sh"
zinit light "b4b4r07/enhancd"

#
# History
#
HISTFILE=$ZDOTDIR/.zsh-history
HISTSIZE=100000
SAVEHIST=1000000

setopt hist_ignore_all_dups
setopt hist_ignore_dups
setopt hist_ignore_space
setopt inc_append_history
setopt share_history

#
# Appearance
#
zinit ice depth=1; zinit light romkatv/powerlevel10k
# To customize prompt, run `p10k configure` or edit ~/.config/zsh/p10k.zsh.
[[ ! -f ~/.config/zsh/p10k.zsh ]] || source ~/.config/zsh/p10k.zsh

### diff
zinit ice as"program" from"gh-r" pick"*/delta"
zinit light dandavison/delta

### fish like
zinit light zsh-users/zsh-autosuggestions
zinit light zdharma/fast-syntax-highlighting
zinit light zdharma/history-search-multi-word
